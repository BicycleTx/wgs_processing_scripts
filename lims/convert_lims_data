#!/usr/bin/perl
use strict;
use warnings;

use Data::Dumper;
use File::Basename;
use Getopt::Long;
use File::Slurp;
use JSON::XS;
use Text::CSV;
use File::Copy;
use 5.01000;

use constant EMPTY => q{ };
use constant NACHAR => 'NA';

my %opt = ();
GetOptions (
    "out_file=s" => \$opt{ out_file },
    "lims_dir=s" => \$opt{ lims_dir },
    "execute"    => \$opt{ execute },
    "debug"      => \$opt{ debug },
    "help|h"     => \$opt{ help },
) or die("Error in command line arguments\n");

my $CNTR_TSV = '/data/common/dbs/sbp/center2entity.tsv';
my $LIMS_DIR = $opt{ lims_dir } || '/data/lims';
my $JSON_OUT = $opt{ out_file } || $LIMS_DIR . '/lims.json';
my $SUBM_OUT = $LIMS_DIR . '/submissions.json';
my $BACK_DIR = $LIMS_DIR . '/backup';
my $CPCT_CSV = $LIMS_DIR . '/latest/lims_cpct';
my $SUBM_TSV = $LIMS_DIR . '/latest/lims_subm';
my $SAMP_TSV = $LIMS_DIR . '/latest/lims_samp';
my $PROC_TSV = $LIMS_DIR . '/latest/lims_proc';

## Closed files from previous years
my $SUBM_TSV_2018 = $LIMS_DIR . '/latest/2018_subm';
my $SAMP_TSV_2018 = $LIMS_DIR . '/latest/2018_samp';
my $PROC_TSV_2018 = $LIMS_DIR . '/latest/2018_proc';
my $PROC_TSV_2017 = $LIMS_DIR . '/latest/2017_proc';
my $LIMS_JSN_2017 = $LIMS_DIR . '/latest/2017_lims.json'; # non-CPCT pre-2018

my @ALL_INPUT_FILES = ( 
  $CNTR_TSV, $CPCT_CSV, $SUBM_TSV, $SAMP_TSV, $PROC_TSV, $SUBM_TSV_2018, 
  $SAMP_TSV_2018, $PROC_TSV_2018, $PROC_TSV_2017, $LIMS_JSN_2017
);

## setup help msg
my $SCRIPT  = `basename $0`; chomp( $SCRIPT );
my $HELP_TEXT = <<"HELP";

  Description
    Parses LIMS excel/csv files and writes to JSON output.
    One object in the json is one sample (by unique 
    sample id/barcode).
    
  Usage
    $SCRIPT -execute
    
  Input files that are used
       centers: $CNTR_TSV
     cpct lims: $CPCT_CSV
     subm lims: $SUBM_TSV
     samp lims: $SAMP_TSV
     proc lims: $PROC_TSV
     subm 2018: $SUBM_TSV_2018
     samp 2018: $SAMP_TSV_2018
     proc 2018: $PROC_TSV_2018
     proc 2017: $PROC_TSV_2017
     lims 2017: $LIMS_JSN_2017

  Output files:
    $JSON_OUT
    $SUBM_OUT
HELP


## ---------- 
## INPUT CHECKS and BACKUP
## ----------
die $HELP_TEXT . "\n" if $opt{ help };
die $HELP_TEXT . "\n" unless $opt{ execute };

foreach ( $BACK_DIR ){
    die "[EXIT] Dir does not exist ($_)\n" unless -d $_;
}
foreach ( @ALL_INPUT_FILES ){
    die "[EXIT] File does not exist ($_)\n" unless -f $_;
}
foreach ( $JSON_OUT ){
    next unless -f $JSON_OUT;
    copy( $_, "$BACK_DIR" ) or die "[EXIT] Backup of \"$_\" to $BACK_DIR failed: $!";
}

    
## ---------- 
## MAIN
## ----------

say "[INFO] START with \"$SCRIPT\"";

my $name_dict = getFieldNameTranslations();
my $cntr_dict = parseDictFile( $CNTR_TSV, 'center2centername' );
my $proc_objs = {}; # will contain objects from InProcess sheet
my $subm_objs = {}; # will contain objects from Received-shipments sheet
my $samp_objs = {}; # will contain objects from Received-samples sheet
my $cpct_objs = {}; # will contain objects from CPCT access DB
my $lims_objs = {}; # will contain all objects joined together

my $lims_2017 = readJson( $LIMS_JSN_2017 );
$samp_objs = $lims_2017->{ 'samples' };
$subm_objs = $lims_2017->{ 'submissions' };

$proc_objs = parseTsv( $proc_objs, $name_dict->{'PROC_CURR'}, 'sample_id',  0, $PROC_TSV_2017, "\t" );
$proc_objs = parseTsv( $proc_objs, $name_dict->{'PROC_CURR'}, 'sample_id',  0, $PROC_TSV_2018, "\t" );
$proc_objs = parseTsv( $proc_objs, $name_dict->{'PROC_CURR'}, 'sample_id',  0, $PROC_TSV, "\t" );
$subm_objs = parseTsv( $subm_objs, $name_dict->{'SUBM_CURR'}, 'submission', 0, $SUBM_TSV_2018, "\t" );
$subm_objs = parseTsv( $subm_objs, $name_dict->{'SUBM_CURR'}, 'submission', 0, $SUBM_TSV, "\t" );
$samp_objs = parseTsv( $samp_objs, $name_dict->{'SAMP_2018'}, 'sample_id',  1, $SAMP_TSV_2018, "\t" );
$samp_objs = parseTsv( $samp_objs, $name_dict->{'SAMP_CURR'}, 'sample_id',  1, $SAMP_TSV, "\t" );
$cpct_objs = parseTsv( $cpct_objs, $name_dict->{'CPCT_CURR'}, 'dna_sample_id_1',  1, $CPCT_CSV, "," );

$lims_objs = processExcelSamples( $lims_objs, $samp_objs, $subm_objs );
$lims_objs = processAccessSamples( $lims_objs, $cpct_objs, $cntr_dict );
$lims_objs = addLabSopString( $lims_objs, $proc_objs );

printLimsToJson( $lims_objs, $subm_objs, $JSON_OUT, $SUBM_OUT );

say "[INFO] DONE with \"$SCRIPT\"";

## ---------- 
## /MAIN
## ----------



## ---------- 
## SUBs
## ----------
sub parseTsv{
    my ($objects, $fields, $store_field_name, $should_be_unique, $file, $sep) = @_;
    my $csv = Text::CSV->new({ binary => 1, auto_diag => 1, sep_char => $sep });
    
    my %store = %$objects;
    say "[INFO]   Parsing input file $file";
        
    ## Check header for completeness
    open IN, "<", $file or die "Unable to open file ($file): $!\n";
    
    my $header_line = <IN>; chomp($header_line);
    die "[EXIT] Cannot parse line ($header_line)\n" unless $csv->parse($header_line);
    my @header_fields = $csv->fields();
    my %fields_map = map { $_ => 1 } @header_fields;
    
    ## checking header
    my $header_misses_field = 0;
    foreach my $field (keys %$fields) {
        if ( not exists $fields_map{ $field } ){
            warn "[WARN] Missing header field ($field) in file ($file)\n";
            $header_misses_field = 1;
        }
    }
    print Dumper \%fields_map and die "[EXIT] Error: header incomplete ($file)\n" if $header_misses_field;
    
    while ( <IN> ){
        chomp;
        die "[EXIT] Cannot parse line ($_)\n" unless $csv->parse($_);
        my @values = $csv->fields();
        
        my %raw_object = ();
        foreach my $field ( @header_fields ){
            my $next_value = shift @values;
            $next_value = NACHAR if not defined $next_value;
            $raw_object{ $field } = $next_value;
        }
        
        my $obj = selectAndRenameFields( \%raw_object, $fields );        
        my $key1 = $obj->{ $store_field_name } || NACHAR;
        my $key2 = $obj->{ 'sample_id' } || NACHAR;
        my $key3 = $obj->{ 'sample_name' } || NACHAR;
        my $key;
        
        ## really try to find a key for storing object
           if ( not isSkipValue($key1) ) { $key = $key1; }
        elsif ( not isSkipValue($key2) ) { $key = $key2; }
        elsif ( not isSkipValue($key3) ) { $key = $key3; }
         else { next; }
        
        ## store key is chosen: do final checks
        next if isSkipValue( $key );
        next if $should_be_unique and not keyIsCorrectAndUnique( \%store, $key);
        
        ## all checks ok: fix and store
        fixDateFields( $obj );
        fixIntegerFields( $obj );
        fixBooleanFields( $obj );
        $store{ $key } = $obj;
    }
    close IN;
    
    return \%store;
}

sub selectAndRenameFields{
    my ($obj_in, $fields) = @_;
    my %obj_out = ();
    foreach my $key ( keys %$obj_in ){
        if ( defined $fields->{ $key } ){
            $obj_out{ $fields->{$key} } = $obj_in->{ $key };
        }
    }
    return \%obj_out;
}

sub readJson{
    my ($json_file) = @_;
    say "[INFO]   Parsing input json file $json_file";
    my $json_txt = read_file( $json_file );
    my $json_obj = decode_json( $json_txt );
    return( $json_obj );
}

sub printLimsToJson{
    my ($samples, $submissions, $lims_file, $subm_file) = @_;
    my $samp_count = scalar keys %$samples;
    my $subm_count = scalar keys %$submissions;

    ## samples and submission json
    my %lims = ( 'samples' => $samples, 'submissions' => $submissions );
    my $coder = JSON::XS->new->utf8->pretty->canonical;
    my $lims_txt = $coder->encode(\%lims);
    
    say "[INFO]   Writing output to $lims_file ($subm_count submissions and $samp_count samples)";
    open my $lims_json_fh, '>', $lims_file or die "Unable to open output file ($lims_file): $!\n";
        print $lims_json_fh $lims_txt;
    close $lims_json_fh;
    
    my $subm_txt = $coder->encode($submissions);
    say "[INFO]   Writing output to $subm_file ($subm_count submissions)";
    open my $subm_json_fh, '>', $subm_file or die "Unable to open output file ($subm_file): $!\n";
        print $subm_json_fh $subm_txt;
    close $subm_json_fh;
}

sub addLabSopString{
    my ($samples, $inprocess) = @_;
    my %store = %$samples;
    my $sop_field_name = 'lab_sop_versions';
    foreach my $id ( keys %store ){
        if ( exists $inprocess->{ $id } ){
            ## format: PREP(\d+)V(\d+)-QC(\d+)V(\d+)-SEQ(\d+)V(\d+)
            $store{ $id }{ $sop_field_name } = $inprocess->{ $id }{ $sop_field_name };
        }
        elsif ( defined $samples->{ $id }{ $sop_field_name } ){
            ## keep whatever is present
        }
        else{
            ## fallback to NA default
            $store{ $id }{ $sop_field_name } = NACHAR;
        }
    }
    return \%store;
}

sub parseDictFile{
    my ($file, $fileType) = @_;
    say "[INFO]   Parsing input dictionary file $file";
    my %store = ();
    
    open my $dict_fh, "<", $file or die "$!: Unable to open file ($file)\n";
    while ( <$dict_fh> ){
        next if /^#/ms;
        chomp;
        if ( $fileType eq 'center2centername' ){
            my ( $id, $descr, $name ) = split /\t/;
            die "[EXIT] id occurs multiple times ($id) in file ($file)\n" if exists $store{ $id };
            $store{ $id } = $name if ( $id ne EMPTY and $name ne EMPTY );
        }
        elsif ( $fileType eq 'submission2entity' ){
            my ( $submission, $entity ) = split /\t/;
            die "[EXIT] submission occurs multiple times ($submission) in file ($file)\n" if exists $store{ $submission };
            $store{ $submission } = $entity if ( $submission ne EMPTY and $entity ne EMPTY );
        }
        else{
            die "[EXIT] Filetype not set or not recognized ($fileType)\n";
        }
    }
    close $dict_fh;
    
    return \%store;
}

sub processAccessSamples{
    
    my ($lims, $objects, $centers_dict) = @_;
    my %store = %{$lims};
    my %name2id = ();
    
    while ( my($sample_id, $row_info) = each %$objects ){
        
        ## first we work on the dna sample
        my $sample_name  = $row_info->{ 'sample_name' };
        my $patient_name = $row_info->{ 'patient' };
        my $main_id      = $row_info->{ 'dna_sample_id_1' };
        
        ## skip samples when LAB gave certain sample name
        checkDefined( 'sample_name', $row_info );
        next if not $sample_name or isSkipValue( $sample_name );
        
        ## skip particular case of weird historic sample naming
        next if $sample_name =~ /^(CPCT\d{8}|DRUP\d{8}|PMC\d{6})A/ms;
        
        ## all other samples names should follow certain formats
        if ( $sample_name =~ /^((CPCT|DRUP|WIDE|CORE)\d{2}([0-9A-Z]{2})\d{4})(T|R){1}/ms ){
            my ($patient, $study_type, $centerid, $tum_or_ref) = ($1, $2, $3, $4);
                
            $row_info->{ 'label' }         = $study_type;
            $row_info->{ 'patient' }       = $patient;
            $row_info->{ 'analysis_type' } = $tum_or_ref eq 'T' ? 'Somatic_T' : 'Somatic_R';
            
            ## CORE is handled per case/submission
            if ( $study_type eq "CORE" ){
                if ( exists $row_info->{ 'submission' } and $row_info->{ 'submission' } =~ /^HMFreg/ ){
                    $row_info->{ 'entity' } = $row_info->{ 'submission' };
                }
                else{
                    warn "[WARN] SKIPPING CORE sample ($sample_name): no correct HMFreg submission present\n";
                    next;
                }
            }
            ## CPCT/DRUP/WIDE are handled study/center wide
            else{
                if ( exists $centers_dict->{ $centerid } ){
                    my $centername = $centers_dict->{ $centerid };
                    $row_info->{ 'submission' } = 'HMFreg' . $study_type;
                    $row_info->{ 'entity' } = join( "_", $study_type, $centername );
                }
                else {
                    warn "[WARN] SKIPPING sample ($sample_name): Unkown center id ($centerid)\n";
                    next;
                }
            }
        }
        elsif ( $sample_name =~ /^(PMC\d{6})(T|R){1}/ms ){
            my ($patient, $tum_or_ref) = ($1, $2);
            $row_info->{ 'label' }         = 'PMC';
            $row_info->{ 'patient' }       = $patient;
            $row_info->{ 'submission' }    = "HMFregPMC";
            $row_info->{ 'entity' }        = "COHORT_PMC01";
            $row_info->{ 'analysis_type' } = $tum_or_ref eq 'T' ? 'Somatic_T' : 'Somatic_R';
        }
        else{
            warn "[WARN] Skipped sample $sample_name from Access lims because format is not as expected\n";
            next;
        }
        
        ## completeness checks
        die "[EXIT] No submission defined for sample ($sample_name)\n" unless $row_info->{ 'submission' };
        die "[EXIT] No analysis type defined for sample ($sample_name)\n" unless $row_info->{ 'analysis_type' };
        $row_info->{ 'project_name' } = $row_info->{ 'submission' };
        
        ## in case of low tumor sample there is no DNA ID present
        ## but want to keep sample for low tumor percentage check
        if ( isSkipValue( $main_id ) ){
            if ( $sample_name =~ /^(CPCT|DRUP|WIDE|CORE|PMC)/ and $sample_name !~ /T0$/ ){
                my $unique = $sample_name;
                next if $unique eq '';
                next if isSkipValue( $unique );
                next unless keyIsCorrectAndUnique( \%store, $unique );
                $row_info->{ 'sample_id' } = $unique;
                $store{ $unique } = $row_info;
            }
            else{
                ## cannot save so completely skip sample
                next;
            }
        }
        
        ## store every DNA ID present as separate sample
        foreach my $idx ( 1, 2, 3 ){
            
            ## first check the new ID
            my $field_name = 'dna_sample_id_' . $idx;
            my $unique = $row_info->{ $field_name };
            next unless defined $unique;
            next if isSkipValue( $unique );
            next unless keyIsCorrectAndUnique( \%store, $unique );
            
            ## make a copy because we will alter the object
            my %row_info_copy = %{$row_info};
            $row_info_copy{ 'sample_id' } = $unique;
            
            ## store
            $store{ $unique } = \%row_info_copy;
        }

        ## perform extra actions depending on source
        if ( defined $row_info->{ 'sample_source' } ){
            my $source = $row_info->{ 'sample_source' };
            
            ## in case of BIOPSY we also store rna sample
            if ( $source =~ m/^Tissue$/i ){
                
                ## store every RNA ID present as separate sample
                foreach my $idx ( 1, 2, 3 ){
                    
                    ## first check the new ID
                    my $field_name = 'rna_sample_id_' . $idx;
                    my $unique = $row_info->{ $field_name };
                    next unless defined $unique;
                    next if isSkipValue( $unique );
                    next unless keyIsCorrectAndUnique( \%store, $unique );
                    
                    ## make a copy because we will alter the object
                    my %row_info_copy = %{$row_info};
                    
                    ## reset and store
                    $row_info_copy{ 'sample_id' } = $unique;
                    $row_info_copy{ 'analysis_type' } = 'RnaAnalysis';
                    $store{ $unique } = \%row_info_copy;
                }
            }
            ## in case of BLOOD we also store plasma sample       
            elsif( $source =~ m/^Blood$/i ){
                
                ## link name to id (used later to add ref sample id to each biopsy)
                if ( exists $name2id{ $sample_name } ){
                    my $existing_id = $name2id{ $sample_name };
                    warn "[WARN] Sample $sample_name ($existing_id) will be overwritten by $sample_name ($sample_id): check with LAB to fix\n";
                }
                $name2id{ $sample_name } = $sample_id;
                
                ## store plasma sample if present and is OK
                next unless defined $row_info->{ 'plasma_sample_id' };
                my $unique = $row_info->{ 'plasma_sample_id' };
                next if isSkipValue( $unique );
                next unless keyIsCorrectAndUnique( \%store, $unique );
                
                ## make a copy because we will alter the object
                my %row_info_copy = %{$row_info};
                
                ## reset and store
                $row_info_copy{ 'sample_id' } = $unique;
                $row_info_copy{ 'analysis_type' } = 'PLASMA';
                $store{ $unique } = \%row_info_copy;
                
            }
            else{
                die "[EXIT] Unexpected value for source ($source), expected TISSUE or Blood (sample \"$sample_name\")\n";
            }
        }
        else{
            warn "[WARN] No source available for sample ($sample_name) so unable to check for RNA/Plasma\n";
        }
    }
    
    ## add R sample_id for each biopsy
    foreach my $sample_id ( keys %store ){
        my $sample = $store{ $sample_id };
        next unless $sample->{ analysis_type } eq 'Somatic_T';
        next unless $sample->{ label } =~ /^(CPCT|DRUP|WIDE|CORE)$/;
        
        my $patient = $sample->{ 'patient' };
        my $ref_sample_name = $patient . 'R';
        my $ref_sample_id = $sample->{ 'ref_sample_id' };
        
        #if ( $ref_sample_id eq "" and $ref_sample_id ne NACHAR ){
        if ( $ref_sample_id eq "" ){
            if ( exists $name2id{ $ref_sample_name } ){
                $ref_sample_id = $name2id{ $ref_sample_name };
                $sample->{ 'ref_sample_id' } = $ref_sample_id;
            }
        }
    }
    
    return \%store;
}

sub processExcelSamples{
    
    my ($lims, $objects, $shipments) = @_;
    my %store = %{$lims};
    my %name2id = ();
    
    ## open file and check header before reading data lines
    while ( my($sample_id, $row_info) = each %$objects ){        
        
        my $sample_name = $row_info->{ 'sample_name' } or die "No sample_name in row_info";
        next if isSkipValue( $sample_name );

        my $sample_id = $row_info->{ 'sample_id' } or warn "[WARN] SKIPPING sample ($sample_name): No sample_id found" and next;
        my $submission = $row_info->{ 'submission' } or warn "[WARN] SKIPPING sample ($sample_name): No submission found" and next;
        my $analysis_type = $row_info->{ 'analysis_type' } or warn "[WARN] SKIPPING sample ($sample_name): No analysis_type found" and next;

        $row_info->{ 'label' } = 'RESEARCH';
        $row_info->{ 'patient' } = $row_info->{ 'sample_name' };
        $row_info->{ 'entity' } = $row_info->{ 'submission' };
        
        ## check data analysis type and set accordingly
        if ( $analysis_type =~ /^(Somatic_R|Somatic_T|SingleAnalysis|FASTQ|BCL|LabOnly)$/ ){
            ## Already final status so no further action
        }
        elsif ( $sample_name =~ /^(CORE\d{2}\d{6})(T|R){1}/ms ){
            my ($patient, $tum_or_ref) = ($1, $2);
            $row_info->{ 'label' }         = "CORE";
            $row_info->{ 'patient' }       = $patient;
            $row_info->{ 'entity' }        = $submission;
            $row_info->{ 'analysis_type' } = $tum_or_ref eq 'T' ? 'Somatic_T' : 'Somatic_R';
        }
        elsif ( $analysis_type eq 'SomaticAnalysis' or $analysis_type eq 'SomaticsBFX' ){
            ## SomaticsBFX is the old term, SomaticAnalysis the new
            my $partner = $row_info->{ 'ref_sample_id' };
            if ( $partner ne '' and $partner ne NACHAR ){
                $row_info->{ 'analysis_type' } = 'Somatic_T';
            }
            else{
                $row_info->{ 'analysis_type' } = 'Somatic_R';
            }
        }
        elsif ( $analysis_type eq 'GermlineBFX' or $analysis_type eq 'Germline' ){
            ## GermlineBFX is the old term, SingleAnalysis the new
            $row_info->{ 'analysis_type' } = 'SingleAnalysis';
        }
        elsif ( $analysis_type eq 'NoBFX' or $analysis_type eq 'NoAnalysis' or $analysis_type eq '' or $analysis_type eq 'NA' ){
            ## NoBFX is the old term, FASTQ the new
            $row_info->{ 'analysis_type' } = 'FASTQ';
        }
        elsif ( $analysis_type eq 'BCL' ){
            $row_info->{ 'analysis_type' } = 'BCL';
        }
        elsif ( $analysis_type eq 'Labonly' ){
            $row_info->{ 'analysis_type' } = 'LabOnly';
        }
        elsif ( $analysis_type eq 'SNPgenotyping' or $analysis_type eq 'SNP' ){
            $row_info->{ 'analysis_type' } = 'SnpGenotyping';
            next;
        }
        else {
            warn "[WARN] SKIPPING sample ($sample_name): has unknown analysis type ($analysis_type)\n";
            next;
        }
        
        ## add submission info and parse KG
        if ( exists $shipments->{ $submission } ){
            my $sub = $shipments->{ $submission };
            my $project_name = $sub->{ 'project_name' };
            $row_info->{ 'project_name' } = $project_name;
            
            if ( $sub->{ 'project_type' } eq 'KG production' ){
                
                my @dvo_parts = split( /\-/, $project_name );
                my $center = uc( $dvo_parts[0] );
                $row_info->{ 'entity' } = 'KG_' . $center;
                $row_info->{ 'label' } = 'KG';
            }
            ## assume that all samples of submission need same analysis
            ## so will just overwrite analysis_type of submission
            $sub->{ 'analysis_type' } = $analysis_type;
        }

        my $unique = $row_info->{ 'sample_id' };
        next if isSkipValue( $unique );

        ## checks before storing
        my $regex = '^[0-9a-zA-Z\-]*$';
        warn "[WARN] SKIPPING sample ($sample_name): sample_name contains unacceptable chars\n" and next if $sample_name !~ /$regex/;
        warn "[WARN] SKIPPING sample ($sample_name): sample_id ($sample_id) contains unacceptable chars\n" and next if $sample_id !~ /$regex/;
        warn "[WARN] SKIPPING sample ($sample_name): no submission defined for sample\n" and next unless $row_info->{ 'submission' };
        warn "[WARN] SKIPPING sample ($sample_name): no analysis type defined for sample\n" and next unless $row_info->{ 'analysis_type' };
        warn "[WARN] SKIPPING sample ($sample_name): no project name defined for sample\n" and next unless $row_info->{ 'project_name' };        
        
        ## store at uniqe id
        next unless keyIsCorrectAndUnique( \%store, $unique );
        $store{ $unique } = $row_info;
        
    }
    
    return \%store;
}

sub fixIntegerFields{
    my ($obj) = @_;
    foreach my $key ( keys %$obj ){
        if ( $obj->{ $key } =~ /^\d+$/ ){
            $obj->{ $key } = $obj->{ $key } + 0;
        }
    }
}

sub fixBooleanFields{
    my ($obj) = @_;
    my @boolean_fields = qw( shallowseq );
    foreach my $key ( @boolean_fields ){
        next unless defined $obj->{ $key };
        my $value = $obj->{ $key };
        if ( $value eq '1' or $value eq '0' ){
            next; # already OK
        }elsif ( $value =~ m/^true$/i ){
            $obj->{ $key } = 1;
        }elsif ( $value =~ m/^false$/i ){
            $obj->{ $key } = 0;
        }else{
            warn "[WARN] Unexpected value ($value) in boolean field ($key)\n"
        }
    }
}

sub fixDateFields{
    my ($obj) = @_;
    my @date_fields = qw( arrival_date sampling_date report_date );
    foreach my $date_field ( @date_fields ){
        
        next unless defined $obj->{ $date_field };
        my $old_date = $obj->{ $date_field };
        my $new_date = $old_date;
        my $identifier = $obj->{ 'sample_name' };
        
        ## date is not always filled in so skip NA fields
        next if isSkipValue( $old_date );
        
        ## Convert all date strings to same format yyyy-mm-dd (eg 2017-01-31)
        if( $old_date =~ /^\w+ (\w{3}) (\d{2}) \d+:\d+:\d+ \w+ (\d{4})$/ ){ 
            ## eg Tue Apr 23 00:00:00 CEST 2019
            my $month_name = $1;
            my $day = $2;
            my $year = $3;
            my $month = getMonthIndexByName( $month_name );
            $new_date = join( "-", $year, $month, $day );
        }
        elsif ( $old_date =~ /^(\d{2})(\d{2})(\d{2})$/ ){
            ## format unclear so need for checks
            warn "[WARN] Date \"$old_date\" in \"$date_field\" has unexpected year ($identifier): please check\n" if ($1 < 8) or ($1 > 20);
            warn "[WARN] Date \"$old_date\" in \"$date_field\" has impossible month ($identifier): please fix\n" if $2 > 12;
            $new_date = join( "-", "20" . $1, $2, $3 );
        }
        elsif ( $old_date =~ /^(\d{2})-(\d{2})-(\d{4})$/ ){
            ## dd-mm-yyyy
            warn "[WARN] Date \"$old_date\" in \"$date_field\" has impossible month ($identifier): please fix\n" if $2 > 12;
            $new_date = join( "-", $3, $2, $1 );
        }
        elsif ( $old_date =~ /^(\d{4})-(\d{2})-(\d{2})$/ ){
            ## case yyyy-mm-dd already ok
            warn "[WARN] Date \"$old_date\" in \"$date_field\" has impossible month ($identifier): please fix\n" if $2 > 12;
        }
        else{
            warn "[WARN] Date string \"$old_date\" in field \"$date_field\" has unknown format for sample ($identifier): kept string as-is but please fix\n";
        }
        
        ## store new format using reference to original location
        $obj->{ $date_field } = $new_date;
    }
}

sub getMonthIndexByName{
    my ($month_name) = @_;
    my %mapping = ( 
        "Jan" => "01", "Feb" => "02", "Mar" => "03", "Apr" => "04",
        "May" => "05", "Jun" => "06", "Jul" => "07", "Aug" => "08",
        "Sep" => "09", "Oct" => "10", "Nov" => "11", "Dec" => "12"
    );
    if ( exists $mapping{ $month_name } ){
        return $mapping{ $month_name };
    }
    else{
        warn "[WARN] Unknown Month name ($month_name): kept as-is but please fix\n";
        return $month_name;
    }
}

sub parseExcelSheet{
    my ($config) = @_;
    
    my $excel = $config->{ 'excel' };
    my $h_val = $config->{ 'h_val' };
    my $h_col = $config->{ 'h_col' };
    my $h_row = $config->{ 'h_row' };
    my $trans = $config->{ 'trans' };
    my $sheet = $config->{ 'sheet' };
    
    say "[INFO] Loading excel file $excel sheet \"$sheet\"";
    my $workbook = Spreadsheet::XLSX->new( $excel ) or die "[EXIT] Unable to load excel file $excel: $!\n";
    my $sheet_obj = $workbook->worksheet( $sheet ) or die "[EXIT] Unable to read sheet \"$sheet\" from file $excel: $!\n";
    
    my @header = ();
    my $max_row = $sheet_obj->{'MaxRow'};
    my $max_col = $sheet_obj->{'MaxCol'};
    
    ## check if header exist where it should be
    my $first_val = EMPTY;
    my $first_cel = $sheet_obj->get_cell( $h_row, $h_col );
    $first_val = $first_cel->unformatted() if defined $first_cel;
    die "[EXIT] Header value ($h_val) cannot be found at set location ($excel)\n" unless $first_val eq $h_val;
    
    ## now read header values for later storage
    foreach my $col ( $h_col .. $max_col ){
        my $cell = $sheet_obj->get_cell( $h_row, $col );
        my $cell_val = NACHAR;
        $cell_val = $cell->unformatted() if defined $cell;
        $cell_val = $trans->{ $cell_val } if defined $trans->{ $cell_val };
        push( @header, $cell_val );
    }
    
    return( \@header, $sheet_obj, $max_row, $max_col );
}

sub isSkipValue{
    my ($value) = @_;
    die "[EXIT] Value to check for skipping is not defined\n" if not defined $value;
    my @to_skip = ( NACHAR, EMPTY, '', 'na', 'naR', 'naT', 'invalid', 'failed', 'nvt', 'no', 'x' );
    foreach my $skip_string ( @to_skip ){
        return 1 if $value =~ /^$skip_string$/i;
    }
    return 0;
}

sub keyIsCorrectAndUnique{
    my ($store, $key) = @_;
    if ( $key =~ /[\n\r]/ ){
        my $woNewlines = $key =~ s/[\n\r\f]/\[ENTER\]/gr;
	warn "[WARN] SKIPPING sample with key ($woNewlines): contains newline/enter\n";
        return 0;
    }
    elsif ( not $key =~ /^[a-zA-z0-9\-]+$/ ){
        warn "[WARN] SKIPPING sample with key ($key): contains characters that are not allowed\n";
        return 0;
    }
    elsif ( exists $store->{ $key } ){
        warn "[WARN] SKIPPING sample with key ($key): key already exists in store\n";
        return 0;
    }
    else{
        return 1;
    }
}

sub checkDefined{
	my ( $key, $hash) = @_;
	if ( not defined $hash->{$key} ){
		warn "[WARN] Value $key is not defined in:\n";
	    print Dumper $hash;
	}
}

sub getFieldNameTranslations{
    ## columns shipments sheet in rest lims (FOR-001)
    my %SUBM_DICT = (
      "Arrival_date"      => 'arrival_date',
      "Project_name"      => 'project_name',
      "HMF_reg"           => 'submission',
      "Requested_product" => 'request',
      "Product_category"  => 'project_type',
      "Sample_count"      => 'sample_count',
      "Lab_is_finished"   => 'has_lab_finished',
      "TAT_lab"           => 'turn_around_time',
      "Contact_name"      => 'contact_name',
      "Contact_email"     => 'contact_email',
      "Remarks"           => 'remarks',
      "Storage_status"    => 'lab_storage_status',
    );

    ## columns samples sheet in 2018 rest lims (FOR-001)
    my %SAMP_DICT_2018 = (
      "Sample_ID"         => 'sample_id',
      "Sample_name"       => 'sample_name',
      "DNA_conc"          => 'dna_conc',
      "Yield"             => 'yield',
      "Q30"               => 'q30',
      "Analysis_type"     => 'analysis_type',
      "Partner_sample"    => 'ref_sample_id',
      "HMF_reg"           => 'submission',
      "SNP_required"      => 'is_snp_required',
      "SNP_exp"           => 'snp_experiment_id',
      "Requested_product" => 'request',
      "State"             => 'lab_status', # lab status
      "Primary_tumor_type"=> 'ptum',
      "Priority"          => 'priority', 
      "Arival_date"       => 'arrival_date',
      "Remarks"           => 'remarks',
    );

    ## columns samples sheet in CURRENT rest lims (FOR-001)
    my %SAMP_DICT = (
      "Sample_ID"         => 'sample_id',
      "Sample_name"       => 'sample_name',
      "DNA_conc"          => 'dna_conc',
      "Yield"             => 'yield',
      "Q30"               => 'q30',
      "Analysis_type"     => 'analysis_type',
      "Partner_sample"    => 'ref_sample_id',
      "HMF_reg"           => 'submission',
      "SNP_required"      => 'is_snp_required',
      "SNP_exp"           => 'snp_experiment_id',
      "Requested_product" => 'request',
      "State"             => 'lab_status', # lab status
      "Priority"          => 'priority', 
      "Arrival_date"      => 'arrival_date',
      "Remarks"           => 'remarks',
    );

    ## columns In Process sheet (HMF-FOR-002)
    my %PROC_DICT = (
      'Sample_ID'         => 'sample_id', # eg FR12345678
      'Sample_name'       => 'sample_name', # eg CPCT1234567R
      'Diluted_library'   => 'library_id', # eg FR12345678 (THIS WAS "barcode_3nm")
      'Sop_tracking_code' => 'lab_sop_versions',
    );

    ## columns CPCT tracking Access table
    my %CPCT_DICT = (
      'Sampling_date'      => 'sampling_date',
      'Arrival_HMF'        => 'arrival_date',
      'Sample_barcode'     => 'sample_tube_barcode',
      'Patient_name'       => 'patient',
      'Sample_name'        => 'sample_name',
      'Source'             => 'sample_source',
      'Yield'              => 'yield',
      'Sample_ID_DNA'      => 'dna_sample_id_1',
      'Sample_ID_DNA_2'    => 'dna_sample_id_2',
      'Sample_ID_DNA_3'    => 'dna_sample_id_3',
      'Sample_ID_RNA'      => 'rna_sample_id_1',
      'Sample_ID_RNA_2'    => 'rna_sample_id_2',
      'Sample_ID_RNA_3'    => 'rna_sample_id_3',
      'Pathology_exp'      => 'pathology_exp',
      'Qiasymphony_exp'    => 'qiasymphony_exp',
      'DNA_prep'           => 'dna_prep',
      'RNA_prep'           => 'rna_prep',
      'Purity_shallow_1'   => 'purity_shallow_1',
      'Purity_shallow_2'   => 'purity_shallow_2',
      'Purity_shallow_3'   => 'purity_shallow_3',
      'Sample_ID_Plasma'   => 'plasma_sample_id',
      'Primary_tumor_type' => 'ptum',
      'tumor_'             => 'tumor_perc', # % in tumor_% is absent in export
      'DNA_conc'           => 'dna_conc',
      'Final_lab_status'   => 'lab_status',
      'Reason_failed'      => 'reason_failed',
      'Status_prep'        => 'prep_status',
      'Remarks'            => 'lab_remarks',
      'Other_Ref'          => 'other_ref',
      'Sample_ID_DNA_ref'  => 'ref_sample_id',
      'Hospital_patient_ID'=> 'hospital_patient_id',
      'Germline_findings'  => 'germline_findings',
      'Requester_email'    => 'requester_email',
      'Requester_name'     => 'requester_name',
      'Submission_number'  => 'submission',
      'ShallowSeq'         => 'shallowseq',
      'Purity'             => 'purity',
      'Report_date'        => 'report_date',
      'Date_of_birth'      => 'date_of_birth',
      'Hospital_PA_Sample_ID' => 'hospital_pa_sample_id',
      'Matching_other_HMF_patient_ID' => 'matching_other_HMF_patient_id'
    );
    
    my %translations = (
        'SUBM_CURR' => \%SUBM_DICT,
        'SAMP_CURR' => \%SAMP_DICT,
        'SAMP_2018' => \%SAMP_DICT_2018,
        'PROC_CURR' => \%PROC_DICT,
        'CPCT_CURR' => \%CPCT_DICT,
    );
    
    return \%translations;
}
