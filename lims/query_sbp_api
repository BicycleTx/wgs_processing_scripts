#!/usr/bin/perl
use strict;
use warnings;
use Data::Dumper;
use File::Basename;
use Getopt::Long;
use File::Slurp;
use JSON;
use Number::Format qw(:subs);
use 5.010.000;

## -----
## Global variables
## -----
my $DATETIME = localtime;
my $SCRIPT = basename $0;
my $JSONS_HOME = '/data/lims/sbpfiles';

my %OUT_FIELDS_PER_TYPE = (
  'samples'   => [ 'submission', 'barcode', 'q30', 'yld_req', 'yld', 'status', 'name', 'sbp_id' ],
  'flowcells' => [ 'name', 'createTime', 'flowcell_id', 'sequencer', 'q30', 'yld', 'undet_rds', 'status', 'sbp_id' ],
  'fastq'     => [ 'sample_id', 'name_r1', 'qc_pass', 'q30', 'yld', 'sbp_id' ],
  'sets'      => [ 'name', 'entity', 'ref_sample', 'tumor_sample', 'sbp_id' ],
  'runs'      => [ 'name', 'entity', 'ref_sample', 'tumor_sample', 'bucket', 'status', 'priority', "pipeline", "ini", 'sbp_id' ],
  'entities'  => [ 'name', 'bucket', 'sbp_id' ],
  'inis'      => [ 'name', 'sbp_id' ],
  'linklog'   => [ 'time', 'username', 'entity', 'set', 'portal_name' ],
  'shares'    => [ 'entity_id', 'entity', 'set_id', 'set', 'start_time', 'end_time', 'sbp_id' ],
);
my $available_types = join( ", ", sort keys %OUT_FIELDS_PER_TYPE );

my $delim = "\t";
my $type;
my $match_by_regex;
my $output_as_json;
my @filters = ();

my $HELP =<<HELP;

  Description
    Parses SBP api (for now based on static downloaded jsons!)
    Current json files: $JSONS_HOME (updated every 30 min by sbpext cronjob)

    You can manually update the jsons to latest by:
      - becoming user "sbpext"
      - executing "sbp_api_get_db"
    
    Links (portal links created) are not updated in cronjob:
      - executing "sbp_api_get_links" with required dates
  
  Available tables/types: 
     $available_types
  
  Usage examples
    $SCRIPT -type samples
    $SCRIPT -type samples -filter "barcode=FR13825534"
    $SCRIPT -type samples -filter "tumor_sample=CPCT0201" -regex
    $SCRIPT -type runs -filter "tumor_sample=CPCT0201" -regex -json
    $SCRIPT -type flowcells
    $SCRIPT -type flowcells -filter "status=Pending"
    $SCRIPT -type flowcells -filter "status=Pending|Sequencing"
    $SCRIPT -type linklog
    
  Options
    -delim    <s>  (output delim)
    -filter   <s>  (search string arg)
    -regex         (allow partial match with filter settings)
    -json          (output as json instead of table)
  
  Filter examples
     -filter "submission=HMFreg0001"
     -filter "barcode=FR12345678"
     -filter "entity=CPCT_AVL"
     -filter "tumor_sample=CPCT02410001T"
     -filter "tumor_sample=CPCT02410001" -regex
    
HELP
print $HELP and exit(0) if scalar @ARGV == 0 or $ARGV[0] eq '-h' or $ARGV[0] eq '--help';

## -----
## Gather input
## -----
GetOptions (
    "type=s" => \$type,
    "filter=s@" => \@filters,
    "delim=s" => \$delim,
    "json" => \$output_as_json,
    "regex" => \$match_by_regex,
) or die "Error in command line arguments\n";
warn "[EXIT] No type given?" and exit(0) unless $type;
warn "[EXIT] Type ($type) not supported" and exit(0) unless exists $OUT_FIELDS_PER_TYPE{ $type };

## -----
## MAIN
## -----
my $objects = readJson( $type );
my $filtered_objects = filterObjects( $objects, \@filters );
my $out_fields = $OUT_FIELDS_PER_TYPE{ $type };

if ( $output_as_json ){
    printOutputAsJson( $filtered_objects );
}else{
    printOutput( $filtered_objects, $out_fields );
}

## -----
## /MAIN
## -----

## generic json reader for all types
sub readJson{
    my ($type) = @_;
    my $json_file = "$JSONS_HOME/$type.json";
    my $json_txt = read_file( $json_file );
    my $json_obj = decode_json( $json_txt );
    
    ## STLI: optimize some fields for viewing
    foreach my $obj ( @$json_obj ){
        $obj->{ 'sbp_id' } = $obj->{ 'id' } if defined $obj->{ 'id' };
        $obj->{ 'q30' } = sprintf( "%.1f", $obj->{ 'q30' } ) if defined $obj->{ 'q30' };
        $obj->{ 'yld' } = format_number( $obj->{ 'yld' } / 1000000, 0 ) if defined $obj->{ 'yld' };
        $obj->{ 'yld_req' } = format_number( $obj->{ 'yld_req' } / 1000000, 0 ) if defined $obj->{ 'yld_req' };
        $obj->{ 'undet_rds' } = format_number( $obj->{ 'undet_rds' } / 1000000, 0 ) if defined $obj->{ 'undet_rds' };
        $obj->{ 'time' } =~ s/T.+$// if defined $obj->{ 'time' };
        $obj->{ 'createTime' } =~ s/T.+$// if defined $obj->{ 'createTime' };
    }
    
    ## STLI: add set info for runs (set info is situated one level deeper)
    if ( $type eq 'runs' ){
        foreach my $obj ( @$json_obj ){
            my @keys_to_move = qw( name ref_sample tumor_sample entity_id );
            foreach my $key ( @keys_to_move ){
                $obj->{ $key } = $obj->{ 'set' }{ $key } if defined $obj->{ 'set' }{ $key };
            }
        }
    }
    
    ## STLI: add extra information for sets/runs from other jsons
    if ( $type eq 'sets' or $type eq 'runs' or $type eq 'shares' ){
        my $entities = jsonFileToObject( "$JSONS_HOME/entities.json" );
        my $inis = jsonFileToObject( "$JSONS_HOME/inis.json" );
        my $stacks = jsonFileToObject( "$JSONS_HOME/stacks.json" );
        foreach my $obj ( @$json_obj ){
            $obj->{ 'entity' } = getFieldValueById( $entities, 'name', $obj->{ 'entity_id' } ) if defined $obj->{ 'entity_id' };
            $obj->{ 'ini' } = getFieldValueById( $inis, 'name', $obj->{ 'ini_id' } ) if defined $obj->{ 'ini_id' };
            $obj->{ 'pipeline' } = getFieldValueById( $stacks, 'revision', $obj->{ 'stack_id' } ) if defined $obj->{ 'stack_id' };
        }
    }

    if ( $type eq 'shares' ){
        my $sets = jsonFileToObject( "$JSONS_HOME/sets.json" );
        foreach my $obj ( @$json_obj ){
            $obj->{ 'set' } = getFieldValueById( $sets, 'name', $obj->{ 'set_id' } ) if defined $obj->{ 'set_id' };
        }
    }

    return( $json_obj );
}

sub jsonFileToObject{
    my ($json_file_path) = @_;
    my $json_txt = read_file( $json_file_path );
    my $json_obj = decode_json( $json_txt );
    return( $json_obj );
}

sub getFieldValueById{
    my ($search_objects, $request_field, $search_id) = @_;
    my $return = "NA";
    foreach my $obj ( @$search_objects ){
        if ( $obj->{ 'id' } == $search_id ){
            $return = $obj->{ $request_field } if defined $obj->{ $request_field };
        }
    }
    
    return( $return );
}

sub filterObjects{
    my ($objects, $filters) = @_;
    
    my @out = ();
    my %filter_counts = ();
    
    foreach my $obj ( @$objects ){
        my $do_skip_object = applyFiltersOnObject( $obj, $filters, \%filter_counts );
        push( @out, $obj ) unless $do_skip_object;        
    }
    
    foreach my $filter ( keys %filter_counts ){
        my $count = $filter_counts{ $filter };
        #say "## FILTER: $count filtered away by filter \"$filter\"";
    }
    
    return \@out;
}

sub applyFiltersOnObject{
    my ($object, $filters, $counts) = @_;
        
    foreach my $filter_string ( @$filters ){
        
        my ($name, $include) = split( "=", $filter_string );
                    
        if ( not exists $object->{ $name } ){
            $counts->{ $name.'=KeyNotExists' }++;
            return(1);
        }
        elsif ( not defined $object->{ $name } ){
            $counts->{ $name.'=KeyNotDefined' }++;
            return(1);
        }
        else{
            my $to_match = $object->{ $name };
            my $exact_match = $object->{ $name } eq $include;
            my $regex_match = $object->{ $name } =~ m/$include/i;
            
            if ( not $exact_match and not $match_by_regex ){
                $counts->{ $filter_string }++;
                return(1);
            }
            elsif( not $regex_match ){
                $counts->{ $filter_string }++;
                return(1);
            }
        }
    }
    
    ## all filters applied and still here so return OK response
    return 0;
}

## print output
sub printOutputAsJson{
    my ($objects) = @_;
    my $json_text = encode_json( $objects );
    say $json_text;
}

## print output
sub printOutput{
    my ($objects, $out_fields) = @_;
    my $object_count = scalar @$objects;
    say "## Script: $SCRIPT";
    say "## DateTime: $DATETIME";
    say '## TotalCount: '.$object_count;
    say '#'.join( $delim, @$out_fields );
    foreach my $obj ( @$objects ){
        my @out_values = map( $obj->{$_} || 'NA', @$out_fields );
        say join( $delim, @out_values );
    }
}

## trims whitespace on both ends of a string
sub trim { my $s = shift; $s =~ s/^\s+|\s+$//g; return $s };
